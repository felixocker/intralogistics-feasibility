# query for identifying resources that do not have sink and source
SELECT DISTINCT ?resource ?type ?area ?point ?x ?y ?radius ?r ?neighbor
WHERE {
    {
        ?resource :located_in ?area . 
        ?area a ?type . 
    }
    FILTER NOT EXISTS {
        ?resource :can_transport_from ?placeholder . 
    }
    FILTER NOT EXISTS {
        ?resource :can_transport_to ?placeholder . 
    }
    ?type rdfs:subClassOf* :spatial_region . 
    {
        ?area :specified_by ?point . 
        ?point a :point . 
        ?point :cartesian_coord_x ?x . 
        ?point :cartesian_coord_y ?y . 
        OPTIONAL {
            ?point :geometrically_neighbors ?neighbor . 
            ?neighbor a :point . 
        }
    } UNION {
        ?area a :circle . 
        ?area :specified_by ?radius . 
        ?radius a :radius . 
        ?radius :has_value ?r . 
    } UNION {
        ?area a :point . 
        ?area :cartesian_coord_x ?x . 
        ?area :cartesian_coord_y ?y . 
    }
}
ORDER BY ?resource
